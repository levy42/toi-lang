server = import lib.http_server
http = import http
io = import io
os = import os
template = import template
global table = import table

fn read_all(path)
  f, err = io.open(path, "r")
  if f == nil
    error("cannot open template: " + path)
  content = f.read()
  f.close()
  if content == nil
    return ""
  return content

TEMPLATE_PATH = "examples/webapp/main.ptml"
tmpl_source = read_all(TEMPLATE_PATH)
render_page = template.compile(tmpl_source)

app = server(host="127.0.0.1", port=8082, log_every_requests=20)

get = app.get

@get("/")
fn home()
  ctx = {
    title = "TOI Web App Example",
    now = os.clock(),
    routes = {
      {path = "/", desc = "Template-rendered HTML page"},
      {path = "/api/status", desc = "JSON response"},
      {path = "/not-found", desc = "Explicit 404 example"},
      {path = "/boom", desc = "Forced 500 example"},
      {path = "/missing", desc = "No route -> framework 404"}
    }
  }
  html = render_page(ctx)
  return http.response(200, {["Content-Type"] = "text/html; charset=utf-8"}, html)

@get("/api/status")
fn api_status()
  return {
    ok = true,
    service = "webapp-example",
    ts = os.clock()
  }

@get("/not-found")
fn not_found_example()
  return {
    status = 404,
    headers = {["Content-Type"] = "text/plain; charset=utf-8"},
    body = "Custom Not Found example route"
  }

@get("/boom")
fn boom()
  error("intentional example error for 500")

print "Web app example running on http://127.0.0.1:8082"
print "Try: /, /api/status, /not-found, /boom, /missing"
app.run()
