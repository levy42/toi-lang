local time = import time
local string = import string
local table = import table

fn bench(name, func)
  start = time.clock()
  result = func()
  elapsed = time.clock() - start
  print string.format("%s %f sec", name, elapsed)
  return result

fn bench_math()
  acc = 0
  for i in 1..2000000
    acc = acc + (i * 3.14159) / ((i % 97) + 1)
  return acc

fn bench_string_concat()
  parts = {""}
  for i in 1..20000
    parts[i] = "abc"
  s = string.join("", parts)
  return #s

fn bench_string_ops()
  s = "the quick brown fox jumps over the lazy dog"
  total = 0
  x = ""
  y = ""
  z = ""
  for i in 1..20000
    x = string.upper(s)
    y = string.lower(x)
    z = string.sub(y, 5, 19)
    total = total + #z
  return total

fn bench_table()
  t = {0}
  table.reserve(t, 200000)
  for i in 1..200000
    table.push(t, i * 2)
  sum = 0
  for i in 1..200000
    sum = sum + t[i]
  return sum

fn bench_calls()
  fn add(a, b)
    return a + b
  acc = 0
  for i in 1..2000000
    acc = add(acc, i)
  return acc

fn bench_memory()
  gc
  base = mem()
  t = {}
  for i in 1..200000
    t[i] = string.format("abc%d", i)
  gc
  used = mem() - base
  return used

print("Pua perf (clock):")
bench("math", bench_math)
bench("string concat", bench_string_concat)
bench("string ops", bench_string_ops)
bench("table", bench_table)
bench("calls", bench_calls)
used = bench("memory", bench_memory)
print string.format("memory %d bytes", used)
